alias userSP R1;
userSP = SP;

PTBR = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 14];

alias system_call_num R2;
system_call_num = [([PTBR + 2*((userSP - 5)/512)]*512) + (userSP - 5) % 512];

alias process_table R3;
process_table = PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16;

alias currentPID R4;
currentPID = [SYSTEM_STATUS_TABLE + 1];

[process_table + 13]=SP;
SP = [process_table + 11]*512 - 1;

/////////SEMGET SYSTEM CALL////////////
if(system_call_num == 17) then
     
     [process_table + 9] = 17;

     alias iter R5;

     alias userAreapage R6;
     userAreapage = [process_table + 11];
     

     iter=0;
     while(iter < 8 && [(userAreapage)*512 + 496 + 2*iter] != -1) do
         iter = iter + 1;
     endwhile;    
     
      if(iter == 8) then
        [([PTBR + 2*((userSP - 1)/512)]*512) + (userSP - 1) % 512] = -1;
        [process_table + 9] = 0;
        SP = [process_table + 13];
        ireturn;
      endif;
      
      alias perProcessTable R7;
      perProcessTable = (userAreapage)*512 + 496 + 2*iter;

      [perProcessTable] = 1;//resource identifier
      
      multipush(R1,R2,R3,R4,R5,R6,R7);
      R1 = 6;
      R2 = [SYSTEM_STATUS_TABLE + 1];
      call MOD_0; //Acquire semaphore
      multipop(R1,R2,R3,R4,R5,R6,R7);

      if(R0 == -1) then
         [([PTBR + 2*((userSP - 1)/512)]*512) + (userSP - 1) % 512] = -2;//no free semaphores
         [process_table + 9] = 0;
         SP = [process_table + 13];
         ireturn;
      endif;

      [perProcessTable + 1] = R0;

      [([PTBR + 2*((userSP - 1)/512)]*512) + (userSP - 1) % 512] = iter;   
      [process_table + 9] = 0;
      SP = [process_table + 13];
      ireturn;
endif;


/////////////SEMRELEASE SYSTEM CALL//////////
if(system_call_num == 18) then
    
   [process_table + 9] = 18;
   
   alias userAreapage R5;
   userAreapage = [process_table + 11];
   
   alias semID R6;
   semID = [([PTBR + 2*((userSP - 4)/512)]*512) + (userSP - 4) % 512];
   
   if(semID > 7 || semID < 0 || [(userAreapage)*512 + 496 + 2*semID] == -1) then
     [([PTBR + 2*((userSP - 1)/512)]*512) + (userSP - 1) % 512] = -1;
     [process_table + 9] = 0;
     SP = [process_table + 13];
     ireturn;
   endif;
   
   multipush(R1,R2,R3,R4,R5);
   R1 = 7;
   R2 = [(userAreapage)*512 + 496 + 2*semID + 1];
   R3 = [SYSTEM_STATUS_TABLE + 1];
   call MOD_0; //Release semaphore
   multipop(R1,R2,R3,R4,R5);

   [(userAreapage)*512 + 496 + 2*semID] = -1;
   [(userAreapage)*512 + 496 + 2*semID + 1] = -1;


   [([PTBR + 2*((userSP - 1)/512)]*512) + (userSP - 1) % 512] = 0;

   SP = [process_table + 13];
   [process_table + 9] = 0;
   ireturn;
endif;    




