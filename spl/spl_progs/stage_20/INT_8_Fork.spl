// Set the MODE FLAG in the process table to system call number of Fork.
alias parentProcessTableEntryAddress R4;
parentProcessTableEntryAddress = PROCESS_TABLE + ([SYSTEM_STATUS_TABLE+1]*16);

[parentProcessTableEntryAddress+ 9] = 8;

// Save user stack value for later use, set up the kernel stack
// Store the value of register SP in the UPTR field of the process table entry of the current process.
[parentProcessTableEntryAddress + 13] = SP;

// Initialize SP (kernel stack pointer) to (user area page number)*512 -1.
SP = [parentProcessTableEntryAddress + 11] * 512  - 1 ;
PTBR = [parentProcessTableEntryAddress + 14];
PTLR = [parentProcessTableEntryAddress + 15];

//Allocate memory and set the Process Table
// Find a free Process Table entry by invoking the get_pcb_entry() function in Process Manager module. 
// If no free entry is found, return -1.

multipush(R4);
R1 = GET_PCB_ENTRY;
call MOD_1;
multipop(R4);

// If the return value is -1, then no free entry is found.
if(R0 == -1)then
    // Set the MODE FLAG in the process table to user mode.
    [parentProcessTableEntryAddress + 9] = 0;
    // Set the return value in the user stack to -1.
    alias userSP_MinusOne R1;
    userSP_MinusOne = [parentProcessTableEntryAddress + 13]-1;
    [[PTBR + 2*((userSP_MinusOne)/512)] * 512 + ((userSP_MinusOne)%512)] = -1;

    // Set the user stack pointer to the value stored in the UPTR field of the process table entry of the current process.
    SP = [parentProcessTableEntryAddress + 13];
    // Return to user mode.
    ireturn;
endif;

alias childPID R1;
alias childPTBR R2;
childPID = R0;

childPTBR = [PROCESS_TABLE + (childPID * 16) + 14];
alias childProcessTableEntryAddress R3;
childProcessTableEntryAddress = PROCESS_TABLE + childPID*16;


// The parent and the child must share the heap.  Hence, if heap pages were not allocated for the parent so far, allocate it now.
// If heap pages are not allocated for the parent process,then allocate 2 heap pages for the parent using the get_free_page() function in Memory Manager module and set the page table entries for the heap pages of the parent.

//Heap
alias heapPageIter R5;
heapPageIter = 0;
while(heapPageIter < 2) do
    if([PTBR+4+2*heapPageIter] ==-1) then
        multipush(R1,R2,R3,R4);
        R1 = 1; // get free page function number
        call MOD_2; // Memory Manager Module
        multipop(R1,R2,R3,R4);
        [PTBR+4+2*heapPageIter] = R0;
        [PTBR+5+2*heapPageIter] = "0110";
    endif;
    heapPageIter = heapPageIter + 1;
endwhile;


// Invoke the get_free_page() function in Memory Manager module to obtain 3 memory pages: 2 for user stack and 1 for User Area Page of the child process.
// User Area Page of the child process
multipush(R1,R2,R3,R4);
R1 = 1; // get free page function number
call MOD_2; // Memory Manager Module
multipop(R1,R2,R3,R4);
[childProcessTableEntryAddress+ 11]=R0;



//Stack
alias stackPageIter R5;
stackPageIter = 0;
while(stackPageIter < 2) do
    multipush(R1,R2,R3,R4);
    R1 = 1; // get free page function number
    call MOD_2; // Memory Manager Module
    multipop(R1,R2,R3,R4);
    [childPTBR+16+2*stackPageIter] = R0;
    [childPTBR+17+2*stackPageIter] = "0110";
    stackPageIter = stackPageIter + 1;
endwhile;


// Copy the parent's Process Table Entry except TICK, PID, PPID, USER AREA PAGE NUMBER, KERNEL STACK POINTER, INPUT BUFFER, PTBR and PTLR to the child.


[childProcessTableEntryAddress + 3] = [parentProcessTableEntryAddress + 3]; // USERID
// [childProcessTableEntryAddress + 5] = [parentProcessTableEntryAddress + 5]; // WAIT FOR WHAT
[childProcessTableEntryAddress + 6] = [parentProcessTableEntryAddress + 6]; // SWAP FLAG
[childProcessTableEntryAddress + 7] = [parentProcessTableEntryAddress + 7]; // INODE INDEX
[childProcessTableEntryAddress + 10] = [parentProcessTableEntryAddress + 10]; // USER AREA SWAP STATUS
[childProcessTableEntryAddress + 13] = [parentProcessTableEntryAddress + 13]; // USER STACK POINTER

// Set the PPID field of child process to the current PID. Also set User Area Page Number to the new UA Page, MODE, TICK and Kernel Stack Pointer to 0. 
[childProcessTableEntryAddress+ 2] = [SYSTEM_STATUS_TABLE + 1]; // PPID
[childProcessTableEntryAddress+ 0] = 0; // TICK
[childProcessTableEntryAddress+ 9] = 0; // MODE FLAG
[childProcessTableEntryAddress+ 12] = 0; // KERNEL STACK POINTER
// Set state of child process to (CREATED, _ ).
[childProcessTableEntryAddress+4] = CREATED ;

// Note : Kernel Context of the child process is empty
// Note : PID, PTBR, PTLR fields of the child's process table is initilized by the get_pcb_entry function.
// Copy the per-process resource table 

alias parentPerProcessTableEntry R6;
alias childPerProcessTableEntry R7;
alias count R5;
alias offset R8;
count  = 0;
while(count<16) do 
    offset =  -16+2*count;
    parentPerProcessTableEntry = (([parentProcessTableEntryAddress + 11] + 1) * 512 ) + offset;
    childPerProcessTableEntry = (([childProcessTableEntryAddress + 11] + 1) * 512 ) + offset;
    
    [childPerProcessTableEntry] = [parentPerProcessTableEntry];
    count = count + 1;
endwhile;

// Copy the  per-process disk map table.
alias parentDiskMapTableWord R6;
alias childDiskMapTableWord R7;
count=0; // R5
while(count<10) do
    parentDiskMapTableWord = 	DISK_MAP_TABLE + [SYSTEM_STATUS_TABLE+1]*10 + count;
    childDiskMapTableWord = 	DISK_MAP_TABLE + childPID*10 + count;
    [childDiskMapTableWord] = [parentDiskMapTableWord];
    count = count + 1;
endwhile;

// For every open file of the parent, increment the Open Instance Count in the Open File Table.

// For every semaphore acquired by the parent, increment Process Count in the Semaphore Table.

// The child shares open files and acquired semaphores with the parent 




// Set Page Tables
// Copy the page table entries (code, heap and library) from the parent's page table to the child  /* Shared between parent and child */
// Code and Library Pages must be shared read only 
// For each page shared, increment its value in the Memory Free List
// Set the page table entries for stack to the pages acquired earlier.

// Copying Library (Shared)
alias libraryPageIter R5;
alias libraryPageStartAddressParent R6;
alias libraryPageStartAddressChild R7;
libraryPageIter = 0;

while(libraryPageIter<2) do
    libraryPageStartAddressParent = PTBR+libraryPageIter*2;
    libraryPageStartAddressChild = childPTBR+libraryPageIter*2;
    [libraryPageStartAddressChild] = [libraryPageStartAddressParent];
    [libraryPageStartAddressChild+1] = [libraryPageStartAddressParent+1];
    [MEMORY_FREE_LIST + [libraryPageStartAddressParent]] = [MEMORY_FREE_LIST + [libraryPageStartAddressParent]] + 1;
    libraryPageIter = libraryPageIter + 1;
endwhile;
// Copying Library (Shared) Done


// Copying Heap (Shared)
alias heapPageIter R5;
heapPageIter = 0;
while(heapPageIter<2) do
    [childPTBR+4+heapPageIter*2] = [PTBR+4+heapPageIter*2];
    [childPTBR+4+heapPageIter*2+1] = [PTBR+4+heapPageIter*2+1];
    [MEMORY_FREE_LIST + [PTBR + 4+heapPageIter*2]] = [MEMORY_FREE_LIST + [PTBR + 4+heapPageIter*2]] + 1;
    heapPageIter = heapPageIter + 1;
endwhile;
// Copying Heap (Shared) Done


// Copying Code (Shared)
alias parentCodePagePhyAddress R5;
alias childCodePagePhyAddress R6;
alias codePageIter R7;



codePageIter =0;
while(codePageIter<4) do
    offset =  8 + 2*codePageIter; // R8
    parentCodePagePhyAddress = PTBR + offset ;
    childCodePagePhyAddress = childPTBR + offset;
    [childCodePagePhyAddress] =  [parentCodePagePhyAddress];
    [childCodePagePhyAddress+1] =  [parentCodePagePhyAddress+1];

    if([parentCodePagePhyAddress]  != -1) then
        [MEMORY_FREE_LIST+ [parentCodePagePhyAddress]] =  [MEMORY_FREE_LIST+ [parentCodePagePhyAddress]] +1;
    endif;

    codePageIter = codePageIter+1;
endwhile;

// Copying Code done

// Copying Stack word by word
// Copy word by word the contents of the parent user stack to that of the child.
alias parentStackPagePhyAddress R5;
alias childStackPagePhyAddress R6;
alias stackPageIter R7;
alias wordIter R8;
alias offset R9;
stackPageIter =0;

while(stackPageIter<2) do
    wordIter = 0;
    while(wordIter <512) do
        offset =  16 + 2*stackPageIter;
        parentStackPagePhyAddress = PTBR + offset;
        childStackPagePhyAddress = childPTBR + offset; 
        [[childStackPagePhyAddress]*512+wordIter] =  [[parentStackPagePhyAddress]*512+wordIter];
        wordIter = wordIter+1;
    endwhile;
    stackPageIter = stackPageIter+1;
endwhile;
// Copying Stack word by word Done

// Store the current BP value to the begining of the kernel stack.
// According to the ExpL calling convention, the BP register is not saved in the user stack. 
// This value is saved here so that the context switch module can restore it when the child is run. 

[[childProcessTableEntryAddress + 11] * 512] = BP;

// Set the return value to 0 for the child process
alias childSP R5;
childSP = [childProcessTableEntryAddress+13];
[[childPTBR + 2 * ((childSP-1)/ 512)] * 512 + ((childSP-1) % 512)] = 0;



// The PID of the child process is set as the return value for the parent process
alias parentSP R5;
parentSP = [parentProcessTableEntryAddress+13];
[[PTBR + 2 * ((parentSP-1)/ 512)] * 512 + ((parentSP-1) % 512)] = childPID;


// Set the MODE_FLAG in the process table entry of the parent process to 0.
[parentProcessTableEntryAddress + 9] = 0;


// Restore SP to User SP and return to the parent process.
SP = [parentProcessTableEntryAddress + 13];

ireturn;