
// Function number and current PID are stored in registers R1 and R2. Give meaningful names to these arguments.
alias functionNum R1;
alias currentPID R2;

// Terminal write function has a function number 3. If the functionNum is 3, implement the following steps else return using return statement.

if (functionNum == TERMINAL_WRITE) then
    // Aquire Terminal
    multipush(R1,R2,R3);
    // Store the function number 8 in register R1 and PID of the current process from the System Status table in register R2 (Can use currentPID, as it already contain current PID value).
    R1 = 8; 
    R2 = currentPID;// no use
    call RESOURCE_MANAGER; // MOD_0
    // Ignore the value present in R0 as Acquire Terminal does not have any return value.

    multipop(R1,R2,R3);
    // Print the word in register R3, using the print statement.
    print R3;
    // Release Terminal
    multipush(R1,R2,R3);
    // Store the function number 9 in register R1 and PID of the current process from the System Status table in register R2 (Can use currentPID, as it already contain current PID value).
    R1 = 9; 
    R2 = currentPID;// no use
    call RESOURCE_MANAGER; // MOD_0
    // Ignore the value present in R0 as Acquire Terminal does not have any return value.

    multipop(R1,R2,R3);

endif;





// Terminal read function
if (functionNum == TERMINAL_READ) then
    // Aquire Terminal
    multipush(R1,R2,R3);
    // Store the function number 9 in register R1 and PID of the current process from the System Status table in register R2 (Can use currentPID, as it already contain current PID value).
    R1 = ACQUIRE_TERMINAL;
    R2 = currentPID;
 
    call RESOURCE_MANAGER; // MOD_0
    
    multipop(R1,R2,R3);

    

    read;

    

    [PROCESS_TABLE+currentPID*16 +4 ] = WAIT_TERMINAL;

    // Invoking the Context Switch Module 
    
    multipush(R1,R2,R3);
    
    call SCHEDULER;

    multipop(R1,R2,R3);

    // The logical address of the word where the data has to be stored is in R3. Convert this logical address to physical address.
    alias logicalAddressOfWord R3;
    alias offset R4;
    alias physicalPageNum R5;
    alias physicalAddress R6;
    offset = R3%512;
    physicalPageNum = [PTBR+(R3/512)*2];
    physicalAddress = physicalPageNum * 512 + offset;

    // Store the value present in input buffer field of process table to the obtained physical address of the word.
    [physicalAddress] = [PROCESS_TABLE+ [SYSTEM_STATUS_TABLE+1]*16+8];

endif;





if (functionNum == DISK_LOAD) then
    // Acquires the disk by invoking the Acquire Disk function in the resource manager module (module 0)
    alias memPageNumber R3;
    alias diskBlockNum R4;
    multipush(R0,R1,R2,R3,R4);
    R1 = ACQUIRE_DISK;
    R2 = currentPID;
    call RESOURCE_MANAGER;
    multipop(R0,R1,R2,R3,R4);

    // Ignore the value present in R0 as Acquire Terminal does not have any return value.

    // Set values in DISK STATUS TABLE
    [DISK_STATUS_TABLE+0] = 1; //STATUS (1 word) - specifies whether the disk is free (indicated by 0) or busy (indicated by 1) handling a memory-disk transfer.
    [DISK_STATUS_TABLE+1] = 0; //LOAD/STORE BIT (1 word) - specifies whether the operation being done on the device is a load (indicated by 0) or store (indicated by 1).
    [DISK_STATUS_TABLE+2] = memPageNumber; //MEMORY PAGE NUMBER (1 word) - specifies the page number of the memory page that is being transferred to or from the disk.
    [DISK_STATUS_TABLE+3] = diskBlockNum; //BLOCK NUMBER (1 word) - specifies the block number of the disk that is currently being accessed.
    [DISK_STATUS_TABLE+4] = currentPID; //PID (1 word) - specifies the PID of the process that is currently using the disk.

    // Issue the load statement to initiate a disk block to memory page DMA transfer.
    load(memPageNumber, diskBlockNum);
    // Set the state of the process (with given PID) to WAIT_DISK and invoke the scheduler.
    [PROCESS_TABLE + currentPID*16 + 4 ] = WAIT_DISK;

    // call scheduler
    multipush(R0,R1,R2,R3,R4);
    call SCHEDULER;
    multipop(R0,R1,R2,R3,R4);

endif;





if(functionNum ==  DISK_STORE) then
    // arguements are PID, Page Number, Block Number	
    alias memPageNumber R3;
    alias diskBlockNum R4;

    // Acquire the lock on the disk device by calling the Acquire_Disk() function in the Resource Manager module;
    multipush(R1,R2,R3,R4);
    R1 = ACQUIRE_DISK;
    R2 = currentPID;
    call RESOURCE_MANAGER;
    multipop(R1,R2,R3,R4);

    // Ignore the value present in R0 as Acquire Disk does not have any return value.

    // Set the LOAD/STORE BIT, PAGE NUMBER and BLOCK NUMBER in the Disk Status Table.
    // Set values in DISK STATUS TABLE
    // Set By AQUIRE DISK [DISK_STATUS_TABLE+0] = 1; //STATUS (1 word) - specifies whether the disk is free (indicated by 0) or busy (indicated by 1) handling a memory-disk transfer.
    [DISK_STATUS_TABLE+1] = 1; //LOAD/STORE BIT (1 word) - specifies whether the operation being done on the device is a load (indicated by 0) or store (indicated by 1).
    [DISK_STATUS_TABLE+2] = memPageNumber; //MEMORY PAGE NUMBER (1 word) - specifies the page number of the memory page that is being transferred to or from the disk.
    [DISK_STATUS_TABLE+3] = diskBlockNum; //BLOCK NUMBER (1 word) - specifies the block number of the disk that is currently being accessed.
    // Set By AQUIRE DISK [DISK_STATUS_TABLE+4] = currentPID; //PID (1 word) - specifies the PID of the process that is currently using the disk.


    // Use the store statement to store the memory page to disk;
    store(memPageNumber, diskBlockNum);

    // Set the state of the process (with given PID) to WAIT_DISK and invoke the scheduler.
    [PROCESS_TABLE + currentPID*16 + 4 ] = WAIT_DISK;

    // call scheduler
    multipush(R0,R1,R2,R3,R4);
    call SCHEDULER;
    multipop(R0,R1,R2,R3,R4);
    

endif;

return;